# Licensed under the MIT License
# https://github.com/craigahobbs/calc-script/blob/main/LICENSE


#
# The CalcScript library documentation application
#


#
# url - The library documentation JSON resource URL
# title - The library title
# menuLinks - Optional array of text/URL menu link tuples
#
async function calcScriptLibraryDoc(url, title, menuLinks)
    # URL override?
    url = if(vURL != null, vURL, url)
    title = if(title != null && vURL == null, title, url)

    # Fetch and validate the library documentation resource
    library = schemaValidate(calcScriptLibraryTypes, 'Library', fetch(url))
    if library == null then
        markdownPrint('**Error:** Failed to fetch library documentation resource "' + url + '"')
        return
    endif

    # Render the page
    if vDoc then
        setDocumentTitle(title + ' - Library')
        elementModelRender(schemaElements(calcScriptLibraryTypes, 'Library'))
    else if vName != null then
        calcScriptLibraryFunctionPage(library, title, vName)
    else then
        calcScriptLibraryIndexPage(library, title, menuLinks)
    endif
endfunction


function calcScriptLibraryIndexPage(library, title, menuLinks)
    # Set the page title
    setDocumentTitle(title)
    markdownPrint('# ' + title)

    # Render the index menu
    markdownPrint('', if(vExpand, '[Collapse](', '[Expand](') + calcScriptLibraryURL(objectNew('expand', !vExpand, 'group', '')) + ')')
    foreach menuLink in menuLinks do
        menuLinkText = arrayGet(menuLink, 0)
        menuLinkURL = arrayGet(menuLink, 1)
        markdownPrint('| [' + markdownEscape(menuLinkText) + '](' + menuLinkURL + ')')
    endforeach

    # Group the function documentation
    debugLog('Library has ' + arrayLength(objectGet(library, 'functions')) + ' functions')
    groups = objectNew()
    foreach function in objectGet(library, 'functions') do
        group = objectGet(function, 'group')
        groupFunctions = objectGet(groups, group)
        if groupFunctions == null then
            groupFunctions = arrayNew()
            objectSet(groups, group, groupFunctions)
        endif
        arrayPush(groupFunctions, objectGet(function, 'name'))
    endforeach

    # Render the library function index
    groupKeys = arraySort(objectKeys(groups))
    foreach groupKey in groupKeys do
        # Render the group key link
        if vExpand then
            markdownPrint('## ' + markdownEscape(groupKey))
        else if vGroup != groupKey then
            markdownPrint('', '## [' + markdownEscape(groupKey) + '](' + calcScriptLibraryURL(objectNew('group', groupKey)) + ')')
        else then
            markdownPrint('', '## [' + markdownEscape(groupKey) + '](' + calcScriptLibraryURL(objectNew('group', '')) + ')')
        endif

        # Render the group's function links
        if vExpand || vGroup == groupKey then
            foreach functionName in arraySort(objectGet(groups, groupKey)) do
                markdownPrint('', '[' + markdownEscape(functionName) + '](' + calcScriptLibraryURL(objectNew('name', functionName)) + ')')
            endforeach
        endif
    endforeach
endfunction


function calcScriptLibraryFunctionPage(libDoc, title, functionName)
    # Set the document title
    setDocumentTitle(title + ' - ' + functionName)

    # Function exist?
    functionFound = false
    foreach function in objectGet(libDoc, 'functions') do
        if functionName == objectGet(function, 'name') then
            functionFound = true
            break
        endif
    endforeach
    if !functionFound then
        markdownPrint('', '**Error:** Unknown function "' + functionName + '"')
        return
    endif
    functionGroup = objectGet(function, 'group')

    # Render the page title and menu
    markdownPrint('# ' + title)
    markdownPrint('', '[Index](' + calcScriptLibraryURL(objectNew('name', '')) + '&' + markdownHeaderId(functionGroup) + ')')
    markdownPrint('', '## ' + functionName, '', objectGet(function, 'doc'))

    # Render the functions's argument documentation
    markdownPrint('', '### Arguments')
    arguments = objectGet(function, 'args')
    if arguments != null then
        foreach argument in arguments do
            markdownPrint('', '**' + objectGet(argument, 'name') + ' -**' + stringFromCharCode(160), objectGet(argument, 'doc'))
        endforeach
    else then
        markdownPrint('None')
    endif

    # Render the function's return documentation
    markdownPrint('', '### Returns')
    returnDoc = objectGet(function, 'return')
    markdownPrint('', if(returnDoc != null, returnDoc, 'Nothing'))
endfunction


function calcScriptLibraryURL(args)
    # Arguments overrides
    expand = objectGet(args, 'expand')
    group = objectGet(args, 'group')
    name = objectGet(args, 'name')
    url = objectGet(args, 'url')

    # Variable arguments
    expand = if(expand != null, expand, vExpand)
    group = if(group != null, group, vGroup)
    name = if(name != null, name, vName)
    url = if(url != null, url, vURL)

    # Cleared arguments
    group = if(group != null && stringLength(group) > 0, group)
    name = if(name != null && stringLength(name) > 0, name)

    # Create the link
    parts = arrayNew()
    if(expand != null && expand, arrayPush(parts, 'var.vExpand=1'))
    if(group != null, arrayPush(parts, "var.vGroup='" + encodeURIComponent(group) + "'"))
    if(name != null, arrayPush(parts, "var.vName='" + encodeURIComponent(name) + "'"))
    if(url != null, arrayPush(parts, "var.vURL='" + encodeURIComponent(url) + "'"))
    return if(arrayLength(parts) != 0, '#' + arrayJoin(parts, '&'), '#var=')
endfunction


# The library documentation schema
calcScriptLibraryTypes = schemaParse( \
    '# A library documentation model', \
    'struct Library', \
    '', \
    '    # The library functions', \
    '    Function[len > 0] functions', \
    '', \
    '# A library function', \
    'struct Function', \
    '', \
    '    # The function name', \
    '    string(len > 0) name', \
    '', \
    '    # The function group (e.g. "Math")', \
    '    string(len > 0) group', \
    '', \
    "    # The function's documentation Markdown lines", \
    '    string[len > 0] doc', \
    '', \
    '    # The function arguments', \
    '    optional FunctionArgument[len > 0] args', \
    '', \
    "    # The function return's documentation Markdown lines", \
    '    optional string[len > 0] return', \
    '', \
    '# A function argument', \
    'struct FunctionArgument', \
    '', \
    '    # The argument name', \
    '    string(len > 0) name', \
    '', \
    "    # The argument's documentation Markdown lines", \
    '    string[len > 0] doc' \
)
