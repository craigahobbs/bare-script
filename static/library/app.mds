# Licensed under the MIT License
# https://github.com/craigahobbs/bare-script/blob/main/LICENSE

include <args.mds>


#
# The BareScript library documentation application main entry point
#
# url - The library documentation JSON resource URL
# title - The library title
# menuLinks - Optional array of text/URL menu link tuples
# groupURLs - Optional map of group name to group Markdown content URL ('' is index)
#
async function bareDocMain(url, title, menuLinks, groupURLs):
    # Parse arguments
    args = argsParse(bareDocArguments)
    group = objectGet(args, 'group')
    name = objectGet(args, 'name')
    url = objectGet(args, 'url', url)

    # Render library JSON documentation page?
    if objectGet(args, 'doc'):
        documentSetTitle('Library')
        elementModelRender(schemaElements(bareDocTypes, 'Library'))
        return
    endif

    # Fetch and validate the library documentation resource
    groups = objectNew()
    urls = if(arrayLength(url) != null, url, arrayNew(url))
    for libraryPart, ixLibrary in systemFetch(urls):
        libraryPart = schemaValidate(bareDocTypes, 'Library', libraryPart)
        if libraryPart == null:
            libraryURL = arrayGet(urls, ixLibrary)
            markdownPrint('**Error:** Failed to fetch library documentation resource "' + markdownEscape(libraryURL) + '"')
            return
        endif

        # Group the function documentation
        for function_ in objectGet(libraryPart, 'functions'):
            groupName = objectGet(function_, 'group')
            groupFunctions = objectGet(groups, groupName)
            if groupFunctions == null:
                groupFunctions = arrayNew()
                objectSet(groups, groupName, groupFunctions)
            endif
            arrayPush(groupFunctions, function_)
        endfor
    endfor

    # Add an empty group for any missing group URLs
    for groupName in objectKeys(groupURLs):
        if groupName != '' && !objectHas(groups, groupName):
            objectSet(groups, groupName, arrayNew())
        endif
    endfor

    # Sort the group functions
    for groupName in objectKeys(groups):
        groupFunctions = objectGet(groups, groupName)
        arraySort(groupFunctions, bareDocFunctionCompare)

        # Function requested by name?
        if name != null:
            for function_ in groupFunctions:
                functionName = objectGet(function_, 'name')
                if functionName == name:
                    # Redirect to the actual URL
                    windowSetLocation( \
                        argsURL(bareDocArguments, objectNew('group', objectGet(function_, 'group'), 'name', null), false, functionName) \
                    )
                    return
                endif
            endfor
        endif
    endfor
    if name != null:
        markdownPrint('', '**Error:** Unknown function "' + markdownEscape(name) + '"')
        return
    endif

    # Render the page
    title = if(title != null && !objectHas(args, 'url'), title, url)
    if group != null:
        bareDocGroupPage(title, groupURLs, groups, group)
    else:
        bareDocIndexPage(title, menuLinks, groupURLs, groups)
    endif
endfunction


# # The BareScript library documentation application arguments
bareDocArguments = argsValidate(arrayNew( \
    objectNew('name', 'doc', 'type', 'bool', 'default', false), \
    objectNew('name', 'group'), \
    objectNew('name', 'name'), \
    objectNew('name', 'url', 'global', 'vURL') \
))


# Helper to compare library function objects by name
function bareDocFunctionCompare(f1, f2):
    f1Name = objectGet(f1, 'name')
    f2Name = objectGet(f2, 'name')
    return if(f1Name < f2Name, -1, if(f1Name == f2Name, 0, 1))
endfunction


# Render a library documentation index page
async function bareDocIndexPage(title, menuLinks, groupURLs, groups):
    # Render the menu and title
    documentSetTitle(title)
    if arrayLength(menuLinks) > 0:
        for menuLink, ixMenuLink in menuLinks:
            menuLinkText = arrayGet(menuLink, 0)
            menuLinkURL = arrayGet(menuLink, 1)
            markdownPrint(if(ixMenuLink == 0, '', '| ') + '[' + markdownEscape(menuLinkText) + '](' + urlEncode(menuLinkURL) + ')')
        endfor
        markdownPrint('')
    endif
    markdownPrint('# ' + markdownEscape(title))

    # Render the index Markdown, if any
    if objectHas(groupURLs, ''):
        rootURL = objectGet(groupURLs, '')
        rootMarkdown = systemFetch(rootURL, null, true)
        markdownPrint('', if(rootMarkdown != null, rootMarkdown, '**Error:** Failed to load "' + markdownEscape(rootURL) + '"'))
    endif

    # Render the library function index
    for groupKey in arraySort(objectKeys(groups)):
        markdownPrint('', '## ' + argsLink(bareDocArguments, groupKey, objectNew('group', groupKey), false, argsTopHeaderId))
    endfor
endfunction


# Render a library documentation group page
async function bareDocGroupPage(title, groupURLs, groups, groupName):
    # Set the document title
    documentSetTitle(title + ' - ' + groupName)

    # Group exist?
    if !objectHas(groups, groupName):
        markdownPrint('', '**Error:** Unknown group "' + markdownEscape(groupName) + '"')
        return
    endif

    # Render the menu and title
    markdownPrint( \
        argsLink(bareDocArguments, 'Index', objectNew('group', null)), \
        '', \
        '# ' + markdownEscape(groupName) \
    )

    # Render the group Markdown, if any
    if objectHas(groupURLs, groupName):
        groupURL = objectGet(groupURLs, groupName)
        groupMarkdown = systemFetch(groupURL, null, true)
        markdownPrint('', if(groupMarkdown != null, groupMarkdown, '**Error:** Failed to load "' + markdownEscape(groupURL) + '"'))
    endif

    # Render the group function index
    groupFunctions = objectGet(groups, groupName)
    if arrayLength(groupFunctions) != 0:
        markdownPrint('', '## Function Index')
    endif
    for function_, ixFunction in groupFunctions:
        functionName = objectGet(function_, 'name')
        markdownPrint('- ' + argsLink(bareDocArguments, functionName, null, false, functionName))
    endfor

    # Render the group function documentation
    for function_ in groupFunctions:
        # Render the function header
        markdownPrint( \
            '', \
            '---', \
            '', \
            '## ' + markdownEscape(objectGet(function_, 'name')), \
            '', \
            objectGet(function_, 'doc') \
        )

        # Render the functions's argument documentation
        markdownPrint('', '### Arguments')
        arguments = objectGet(function_, 'args')
        if arguments != null:
            for argument in arguments:
                markdownPrint('', '**' + markdownEscape(objectGet(argument, 'name')) + ' -**', objectGet(argument, 'doc'))
            endfor
        else:
            markdownPrint('', 'None')
        endif

        # Render the function's return documentation
        markdownPrint('', '### Returns')
        returnDoc = objectGet(function_, 'return')
        markdownPrint('', if(returnDoc != null, returnDoc, 'Nothing'))
    endfor
endfunction


# The library documentation schema
bareDocTypes = schemaParse( \
    '# A library documentation model', \
    'struct Library', \
    '', \
    '    # The library functions', \
    '    Function[len > 0] functions', \
    '', \
    '', \
    '# A library function', \
    'struct Function', \
    '', \
    '    # The function name', \
    '    string(len > 0) name', \
    '', \
    '    # The function group (e.g. "Math")', \
    '    string(len > 0) group', \
    '', \
    "    # The function's documentation Markdown lines", \
    '    string[len > 0] doc', \
    '', \
    '    # The function arguments', \
    '    optional FunctionArgument[len > 0] args', \
    '', \
    "    # The function return's documentation Markdown lines", \
    '    optional string[len > 0] return', \
    '', \
    '', \
    '# A function argument', \
    'struct FunctionArgument', \
    '', \
    '    # The argument name', \
    '    string(len > 0) name', \
    '', \
    "    # The argument's documentation Markdown lines", \
    '    string[len > 0] doc' \
)
